---
layout: post
title: "【VB.Net】CallByNameの速度検証"
upload_date: 2020-05-31 23:30:42 +0900
date: 2020-05-31 23:30:42 +0900
categories: ['VB.Net']
permalink: blog/language/VB2eNet/2020/05/31/VB_Net
---

# はじめに
***
今回はVB.NetのCallByNameの参照速度を検証してみる。
CallByNameは指定したインスタンスが所有するメソッドやプロパティを文字列から取得する関数。
遅延評価であるため（？）、べた書きより遅いとされる。ではどのくらい遅いのか。


# 結果
***
プロパティの呼び出しをCallByNameとべた書きでそれぞれ100万回ずつ行った。
結果としてCallByNameはべた書きに対し、100倍以上時間がかかることがわかった。
データ数が少なければ体感として時間差は感じないかもしれないが、レコードが1万、
カラムが100個のテーブルを更新するなんてことがあった場合は、さすがに体感でもわかる時間差になる。

|   手法   | 時間(s) |
| :------: | ------: |
|CallByName| 4.672  |
| べた書き  | 0.044  |


{% highlight python %}
CallMembersWithCallByName ---Start
total(1000000): 4.672 [s]
CallMembersWithCallByName ---End

CallMembers ---Start
total(1000000): 0.044 [s]
CallMembers ---End

CallByName / BetaGaki = 106.18181818181819
{% endhighlight%}


# 付録
今回使用したコード

{% highlight vbnet %}
Imports System


Class TestClass
    Public ReadOnly Property A As String
    Public ReadOnly Property B As String
    Public ReadOnly Property C As String
    Public ReadOnly Property D As String
    Public ReadOnly Property E As String

    Public Sub New()
        A = NameOf(A)
        B = NameOf(B)
        C = NameOf(C)
        D = NameOf(D)
        E = NameOf(E)
    End Sub

    Public Sub Dammy(str As String)

    End Sub

    ''' <summary>
    ''' CallByNameを使用してメンバ変数を呼び出す
    ''' </summary>
    Public Sub CallMembersWithCallByName()
        For Each m In New String() {"A", "B", "C", "D", "E"}
            Dammy(CallByName(Me, m, CallType.Get))
        Next
    End Sub

    ''' <summary>
    ''' 通常通りにメンバ変数を呼び出す
    ''' </summary>
    Public Sub CallMembers()
        For Each m In New String() {A, B, C, D, E}
            Dammy(m)
        Next
    End Sub

    Public Sub CallMembers2()
        For Each m In New String() {A, B, C, D, E}
            Dammy(m)
        Next
    End Sub

End Class

Module Program

    Delegate Sub Processing()

    Function BenchMark(times As Long, subpro As Processing) As Double
        ' 関数名取得
        Dim methodName As String = subpro.Method.Name()
        Console.WriteLine(methodName & " ---Start")

        ' ストップウォッチ
        Dim sw As New Stopwatch()
        sw.Start()
        For i = 1 To times
            subpro.Invoke()
        Next
        sw.Stop()

        ' ミリ秒 to 秒
        Dim seconds As Double = sw.ElapsedMilliseconds / 10 ^ 3
        Console.WriteLine("total(" & times & ")" & seconds & " [s]")

        Console.WriteLine(methodName & " ---End")
        Console.WriteLine()

        Return seconds
    End Function

    Sub Main(args As String())
        ' 処理回数
        Dim times As Long = 10 ^ 6

        ' テストクラス
        Dim clsTest As New TestClass()

        ' ベンチマーク(処理時間(s)を受け取る)
        Dim CallByNameSeconds As Double = BenchMark(times, AddressOf clsTest.CallMembersWithCallByName)
        Dim BetaGakiSeconds As Double = BenchMark(times, AddressOf clsTest.CallMembers)

        ' 倍率を表示
        Console.WriteLine("CallByName / BetaGaki = " & CallByNameSeconds / BetaGakiSeconds)

    End Sub
End Module
{% endhighlight %}